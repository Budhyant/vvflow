#!/usr/bin/awk -f
BEGIN{
	if (ARGC<2) {
		print "Usage: vvawk.avg [-v VAR=VALUE] FILE" > "/dev/stderr"
		print "Calculate the average of a column" > "/dev/stderr"
		print "\tE(x) = sum(x)/N" > "/dev/stderr"
		print "See http://en.wikipedia.org/wiki/Arithmetic_mean" > "/dev/stderr"
		print "Variables:" > "/dev/stderr"
		print "\tcol -- column number to be averaged (default: 2)" > "/dev/stderr"
		print "\txmin, xmax -- X-axis range (default: -inf,+inf)" > "/dev/stderr"
		exit 0
	}
	col =  col ? col+0 : 2
	xmin = xmin ? xmin+0. : -1e400
	xmax = xmax ? xmax+0. : +1e400
}

{
	if($1>xmin && $1<xmax) {
		sum+= $col;
		n++;
	}
}

END {
	printf "%.6e\n", (n>0) ? sum/n : 0;
}