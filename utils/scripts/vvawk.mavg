#!/usr/bin/awk -f
BEGIN{
    if (ARGC<2) {
        print "Usage: vvawk.mavg [-v VAR=VALUE] FILE" > "/dev/stderr"
        print "Perform simple moving average algorithm on a particular column" > "/dev/stderr"
        print "See http://en.wikipedia.org/wiki/Moving_average#Simple_moving_average" > "/dev/stderr"
        print "Variables:" > "/dev/stderr"
        print "\tspan -- number of samples being averaged (default: 20)" > "/dev/stderr"
        print "\tcol -- column number to be averaged (default: 2)" > "/dev/stderr"
        exit 0
    }
    col =  col ? col+0 : 2
    span = span ? span+0 : 20
}

{
    # circular index
    mod = NR % span
    if (NR<=span) count++
    else sum -= array[mod]
    sum+= $col
    array[mod]=$col
    for (i=1; i<col; i++)
        printf "%s%s", $i, OFS
    printf "%.6e%s", sum/count, OFS
    for (i=col+1; i<=NF; i++)
        printf "%s%s", $i, OFS
    printf "%s", RS
}
